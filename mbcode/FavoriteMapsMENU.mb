'*******************************************************************************
'**   Created by Peter Horsbøll Møller, Pitney Bowes Software
'** 	Program:
'** 	Modul:
'**
'*******************************************************************************
'	31-08-2015 11:30:41	- Recompiled and Linked with the latest mbLibrary modules
'*******************************************************************************

'-------------------------------------
Include "MapBasic.def"
Include "Icons.def"
Include "Menu.def"
'**default constants...
Include "Library\Defaults.def"

Define xProgram 		"FavoriteMaps"
Define xVersion 		"1.01"
Define xYear			"2015"
Define xAddToMenuBar	FALSE

Define FILE_INI			ApplicationDirectory$() & xProgram & ".ini"
Define FILE_DBG			ApplicationDirectory$() & xProgram & ".dbg"
'Define FILE_DLL			ApplicationDirectory$() & xProgram & ".dll"
Define FILE_PAD			GetFolderPath$(FOLDER_MI_PREFERENCE) & xProgram & ".btp"

Include "Enums.def"

Include "Library\MI_ICONS_X64.def"
Include "Library\RibbonElements.def"

Include "Library\CONFIGFILELib.def"
Include "Library\DEBUGLib.def"
Include "Library\ERRORLib.def"
Include "Library\FILELib.def"
Include "Library\LAYERLib.def"
Include "Library\MAPPERLib.def"
Include "Library\ProgramInfo.def"
Include "Library\RIBBONLib.def"
Include "Library\Types\T_LAYER_SETTINGS.def"
Include "Library\Types\T_LABEL_SETTINGS.def"
Include "Library\Types\T_LAYER_INFO.def"

Type T_BASE_MAP
	sName 		As String
	sImageFile	As String
	sTabFile		As String
	sLyrSettings	As String
	sLblSettings	As String
End Type

Declare Sub Main
Declare Sub EndHandler

Declare Sub MENUReadConfigFile

Declare Sub MENUOpenFavoriteMap(ByVal nItem As Integer)
Declare Sub MENUOpenFavoriteMap1
Declare Sub MENUOpenFavoriteMap2
Declare Sub MENUOpenFavoriteMap3
Declare Sub MENUOpenFavoriteMap4
Declare Sub MENUOpenFavoriteMap5

Declare Sub MENUSaveLayerSettingsLC

Dim	marrTabs(), marrGroups(), marrAfterControls(), marrBeforeControls() As String,
	marrSplitButtonCtrlIDs(), marrFirstControlIDs() As Integer,
	mnImageSize As Integer,
	mbShowSaveLayerSettingMenu As Logical,
	marrFavoriteMaps() As T_BASE_MAP
'*************************************
Sub Main

Dim	nCtrlIdx, nTab, nCtrl As Integer,
	sSplitButtonName, sSplitButtonGroupName As String

OnError GoTo ErrorOccured

	If NOT SystemInfo(SYS_INFO_MAPINFO_INTERFACE) = MIINTERFACE_RIBBON Then
		Note "Please note that this application has been designed for the Ribbon interface only. It will not work on the classic menubased interface"
		End Program
	End If

	'***Enabling Debug if file exists
	Call DEBUGEnableByFile(FILE_DBG)

	'***Initializing ProgramInfo
	Call PRGISetApplicationName(xProgram)
	Call PRGISetApplicationVersion(xVersion)
	Call PRGISetApplicationCopyrightYear(xYear)
	Call PRGISetApplicationDevelopedBy("Peter Horsbøll Møller")
	Call PRGISetCopyrightCompany("Pitney Bowes")
	Call PRGISetWebPageName("mapinfo.com")
	Call PRGISetWebPageURL("http://www.mapinfo.com")
	Call PRGISetApplicationImageUri(MI_IMG_MAP_ADDBASEMAP_32)
	Call PRGISetDescription("This tools lets you add shortcuts to your favorite maps or tables to the Ribbon interface")

	Call RBNNotificationShow(PRGIGetApplicationName(), "Launching...", Notify_Info, 2000)

	Call MENUReadConfigFile

	If Ubound(marrFavoriteMaps) = 0 Then
		Note "Please add at least one favorite map to the configuration file: " & FILE_INI
		End Program
	ElseIf Ubound(marrTabs) = 0 Then
		Note "Please specify at least one tab to add the favorite maps to using the configuration file: " & FILE_INI
		End Program
	End If

	'**Adding the Favorite Maps SplitButton to the required tabs
	For nTab = 1 To Ubound(marrTabs)
		If marrTabs(nTab) = "tabFavoriteMaps" Then
			'Call DEBUGPrint(nTab & ": Adding custom tab called FAVORITE MAPS")
			If NOT RBNTabExists(marrTabs(nTab)) Then
				nCtrlIdx = RBNAddTab(marrTabs(nTab), "FAVORITE MAPS", "F")
				If nCtrlIdx = 0 Then
					Note "An error occured while adding a new tab called & '" & marrTabs(nTab) & "' to the ribbon!"
					Exit Sub
				End If
			End If

			If NOT RBNTabGroupExists(marrTabs(nTab), marrGroups(nTab)) Then
				nCtrlIdx = RBNTabAddGroup(marrGroups(nTab), "Maps", marrTabs(nTab))
				If NOT nCtrlIdx > 0 Then
					Exit Sub
				End If
			End If

			For nCtrl = 1 To Ubound(marrFavoriteMaps)
				'Call DEBUGPrint(nCtrl & ": Button " & marrFavoriteMaps(nCtrl).sName & " to tab FAVORITE MAPS")
				nCtrlIdx = RBNGroupAddButton("openBaseMap" & nCtrl, marrFavoriteMaps(nCtrl).sName, "", marrTabs(nTab), marrGroups(nTab))
				If nCtrlIdx > 0 Then
					'Create & Set the button tooltip
					Call RBNControlSetToolTipIdx(nCtrlIdx, PRGIGetApplicationName(), "Open basemap '" & marrFavoriteMaps(nCtrl).sName & "'", "")
					'Set the button icon
					Call RBNControlSetIconsIdx(nCtrlIdx, mnImageSize, "", marrFavoriteMaps(nCtrl).sImageFile)
					'Set Custom MapBasic Handle to the button
					Call RBNControlSetCustomMBXHandlerIdx(nCtrlIdx, "MENUOpenFavoriteMap" & nTab)

					If marrFirstControlIDs(nTab) = 0 Then
						marrFirstControlIDs(nTab) = RBNControlGetControlIDIdx(nCtrlIdx)
					End If
				End If
			Next
		Else
			'Adding a splitbutton
			sSplitButtonName	= "sbtn" & marrTabs(nTab) & "BaseMaps"
			If marrAfterControls(nTab) <> "" Then
				'Call DEBUGPrint(nTab & ": inserting splitbutton to the tab called " & marrTabs(nTab) & " after " & marrAfterControls(nTab))
				nCtrlIdx	= RBNGroupInsertControlAfter(sSplitButtonName, "Basemaps", "", ControlType_SplitButton, marrTabs(nTab), marrGroups(nTab), marrAfterControls(nTab))
			ElseIf marrBeforeControls(nTab) <> "" Then
				'Call DEBUGPrint(nTab & ": inserting splitbutton to the tab called " & marrTabs(nTab) & " before " & marrBeforeControls(nTab))
				nCtrlIdx	= RBNGroupInsertControlBefore(sSplitButtonName, "Basemaps", "", ControlType_SplitButton, marrTabs(nTab), marrGroups(nTab), marrBeforeControls(nTab))
			Else
				'Call DEBUGPrint(nTab & ": Adding splitbutton to the tab called " & marrTabs(nTab))
				nCtrlIdx	= RBNGroupAddControl(sSplitButtonName, "Basemaps", "", ControlType_SplitButton, marrTabs(nTab), marrGroups(nTab))
			End If

			If nCtrlIdx > 0 Then
				'Set the button icon
				Call RBNControlSetIconsIdx(nCtrlIdx, mnImageSize, "", marrFavoriteMaps(1).sImageFile)
				'**Set the handler to match the handler of the first button
				Call RBNControlSetCustomMBXHandlerIdx(nCtrlIdx, "MENUOpenFavoriteMap" & nTab)
				'**Get the ID of the SplitButton - nneded in the handler
				marrSplitButtonCtrlIDs(nTab)	= RBNControlGetControlIDIdx(nCtrlIdx)

				sSplitButtonGroupName		= "sbtngrp" & marrTabs(nTab) & "BaseMaps"
				nCtrlIdx	= RBNSplitButtonAddGroup(sSplitButtonGroupName, "", marrTabs(nTab), marrGroups(nTab), sSplitButtonName)

				If nCtrlIdx > 0 Then
					For nCtrl = 1 To Ubound(marrFavoriteMaps)
						'Call DEBUGPrint(nCtrl & ": Button " & marrFavoriteMaps(nCtrl).sName & " to splitbutton called " & sSplitButtonName)
						nCtrlIdx = RBNSplitButtonGroupAddControl(sSplitButtonName & nCtrl, marrFavoriteMaps(nCtrl).sName, ""
									, ControlType_Button, marrTabs(nTab), marrGroups(nTab), sSplitButtonName, sSplitButtonGroupName)
						If nCtrlIdx > 0 Then
							'Create & Set the button tooltip
							Call RBNControlSetToolTipIdx(nCtrlIdx, PRGIGetApplicationName(), "Open basemap '" & marrFavoriteMaps(nCtrl).sName & "'", "")
							'Set the button icon
							Call RBNControlSetIconsIdx(nCtrlIdx, mnImageSize, "", marrFavoriteMaps(nCtrl).sImageFile)
							'Set Custom MapBasic Handle to the button
							Call RBNControlSetCustomMBXHandlerIdx(nCtrlIdx, "MENUOpenFavoriteMap" & nTab)

							If marrFirstControlIDs(nTab) = 0 Then
								marrFirstControlIDs(nTab) = RBNControlGetControlIDIdx(nCtrlIdx)
								Call DEBUGPrint("Tab " & nTab & " has Control " & marrFirstControlIDs(nTab) & " as first control")
							End If
						End If
					Next
				End If
			End If
		End If
	Next

	If mbShowSaveLayerSettingMenu Then
		Alter Menu ID M_SHORTCUT_LC_LAYERS
			Add
				"Save layer settings for Favorite Maps"
					HelpMsg "Save layer setting for Favorite Maps"
					Calling MENUSaveLayerSettingsLC
	End If

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "Main")
	Call ERRShow()

End Sub

'*************************************
Sub Endhandler

OnError GoTo ErrorOccured

	'**Ask the RibbonLib to tidy up before the app ends
	Call RBNEndHandler
	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "Endhandler")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Pitney Bowes Software
'Parameters:
'
'**********************************************************************************************''
Sub MENUSaveLayerSettingsLC

Dim	i, nMID, nLayers, nLayer, nMap, nMapFound As Integer,
	sTabFile As String,
	tlbs As T_LABEL_SETTINGS,
	tls As T_LAYER_SETTINGS

OnError GoTo ErrorOccured

	For i = 1 To LayerControlInfo(LC_INFO_SEL_COUNT)
		nMID		= LayerControlSelectionInfo (i, LC_SEL_INFO_MAPWIN_ID)
		nLayer	= LayerControlSelectionInfo(i, LC_SEL_INFO_LAYER_ID)

		If NOT LayerInfo(nMID, nLayer, LAYER_INFO_TYPE) IN (LAYER_INFO_TYPE_COSMETIC, LAYER_INFO_TYPE_THEMATIC) Then
			sTabFile = TableInfo(LayerInfo(nMID, nLayer, LAYER_INFO_NAME), TAB_INFO_TABFILE)

			For nMap = 1 To Ubound(marrFavoriteMaps)
				If marrFavoriteMaps(nMap).sTabFile = sTabFile Then

					Call tlsGetInfo(nMID, nLayer, tls)
					marrFavoriteMaps(nMap).sLyrSettings = tlsToString(tls)
					Call CONFIGWriteKey(FILE_INI, "FAVORITE MAPS", "LAYERSETTING" & nMap, marrFavoriteMaps(nMap).sLyrSettings)

					Call tlbsGetInfo(nMID, nLayer, tlbs)
					marrFavoriteMaps(nMap).sLblSettings = tlbsToString(tlbs)
					Call CONFIGWriteKey(FILE_INI, "FAVORITE MAPS", "LABELSETTING" & nMap, marrFavoriteMaps(nMap).sLblSettings)

				End If
			Next
		End If
	Next
	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "MENUSaveLayerSettingsLC")
	Call ERRShow()

End Sub

'*************************************
Sub MENUOpenFavoriteMap(ByVal nItem As Integer)

Dim	nMID, nLayer As Integer,
	tli As T_LAYER_INFO

OnError GoTo ErrorOccured

	Call DEBUGPrint("Trying to open item " & nItem)

	Call RBNNotificationShow(PRGIGetApplicationName(), "Opening Base Map '" & marrFavoriteMaps(nItem).sName & "'", Notify_Info, 2000)

	Open Table marrFavoriteMaps(nItem).sTabFile

	nMID = MAPFrontMostWindow()
	If nMID = 0 Then
		Map From PathToTableName$(marrFavoriteMaps(nItem).sTabFile)
		nMID = MAPFrontMostWindow()
		nLayer = 1
	Else
		Add Map
			Window nMID
			Auto
			Layer PathToTableName$(marrFavoriteMaps(nItem).sTabFile)
		nLayer = LAYERGetLayerNumber(nMID, PathToTableName$(marrFavoriteMaps(nItem).sTabFile))
	End If

	If nLayer > 0 Then
		tli.nMID	= nMID
		tli.sName	= PathToTableName$(marrFavoriteMaps(nItem).sTabFile)

		If marrFavoriteMaps(nItem).sLyrSettings <> "" Then
			If tlsFromString(tli.tlsLyrSettings, marrFavoriteMaps(nItem).sLyrSettings) Then

			End If
		End If
		If marrFavoriteMaps(nItem).sLblSettings <> "" Then
			If tlbsFromString(tli.tlbsLblSettings, marrFavoriteMaps(nItem).sLblSettings) Then

			End If
		End If

		If marrFavoriteMaps(nItem).sLyrSettings <> "" OR marrFavoriteMaps(nItem).sLblSettings <> "" Then
			If tliApplyInfo(nMID, nLayer, tli) Then
				Call DEBUGPrint("Layer and/or label settings were applied to the layer!")
			End If
		End If
	End If

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "MENUOpenFavoriteMap: " & nItem)
	Call ERRShow()

End Sub

'*************************************
Sub MENUOpenFavoriteMap1

Dim	nCtrl, nItem As Integer

OnError GoTo ErrorOccured

	nItem	= CommandInfo(CMD_INFO_TOOLBTN)
	nCtrl	= 1

	Call DEBUGPrint("The user clicked on item " & nItem & " The first control for this is " & marrFirstControlIDs(nCtrl))
	If nItem = marrSplitButtonCtrlIDs(nCtrl) Then
		'**User clicked on the initial control on the SplitButton
		nItem	= 1
	Else
		nItem	= nItem - marrFirstControlIDs(nCtrl) + 1
	End If

	Call MENUOpenFavoriteMap(nItem)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "MENUOpenFavoriteMap1")
	Call ERRShow()

End Sub

'*************************************
Sub MENUOpenFavoriteMap2

Dim	nCtrl, nItem As Integer

OnError GoTo ErrorOccured

	nItem	= CommandInfo(CMD_INFO_TOOLBTN)
	nCtrl	= 2

	Call DEBUGPrint("The user clicked on item " & nItem & " The first control for this is " & marrFirstControlIDs(nCtrl))
	If nItem = marrSplitButtonCtrlIDs(nCtrl) Then
		'**User clicked on the initial control on the SplitButton
		nItem	= 1
	Else
		nItem	= nItem - marrFirstControlIDs(nCtrl) + 1
	End If

	Call MENUOpenFavoriteMap(nItem)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "MENUOpenFavoriteMap2")
	Call ERRShow()

End Sub

'*************************************
Sub MENUOpenFavoriteMap3

Dim	nCtrl, nItem As Integer

OnError GoTo ErrorOccured

	nItem	= CommandInfo(CMD_INFO_TOOLBTN)
	nCtrl	= 3

	If nItem = marrSplitButtonCtrlIDs(nCtrl) Then
		'**User clicked on the initial control on the SplitButton
		nItem	= 1
	Else
		nItem	= nItem - marrFirstControlIDs(nCtrl) + 1
	End If

	Call MENUOpenFavoriteMap(nItem)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "MENUOpenFavoriteMap3")
	Call ERRShow()

End Sub

'*************************************
Sub MENUOpenFavoriteMap4

Dim	nCtrl, nItem As Integer

OnError GoTo ErrorOccured

	nItem	= CommandInfo(CMD_INFO_TOOLBTN)
	nCtrl	= 4

	If nItem = marrSplitButtonCtrlIDs(nCtrl) Then
		'**User clicked on the initial control on the SplitButton
		nItem	= 1
	Else
		nItem	= nItem - marrFirstControlIDs(nCtrl) + 1
	End If

	Call MENUOpenFavoriteMap(nItem)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "MENUOpenFavoriteMap4")
	Call ERRShow()

End Sub

'*************************************
Sub MENUOpenFavoriteMap5

Dim	nCtrl, nItem As Integer

OnError GoTo ErrorOccured

	nItem	= CommandInfo(CMD_INFO_TOOLBTN)
	nCtrl	= 5

	If nItem = marrSplitButtonCtrlIDs(nCtrl) Then
		'**User clicked on the initial control on the SplitButton
		nItem	= 1
	Else
		nItem	= nItem - marrFirstControlIDs(nCtrl) + 1
	End If

	Call MENUOpenFavoriteMap(nItem)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "MENUOpenFavoriteMap5")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Pitney Bowes Business Insight
'Parameters:
'
'**********************************************************************************************''
Sub MENUReadConfigFile

Dim	i, nFile, nCount, nNoneFound As Integer,
	sName, sImageFile, sTabFile, sLyrSettings, sLblSettings As String

OnError GoTo ErrorOccured

'[FAVORITE MAPS]
'NAME1=Basemap Colours
'TABFILE1=BaseMaps\BaseMap.tab
'IMAGEFILE1=Images\BaseMap_32x32.png
'LAYERSETTING1=
'LABELSETTING1=

	Call DEBUGPrint("MENUReadConfigFile:" & FILE_INI)
	Do While TRUE
		nFile		= nFile + 1
		sName		= CONFIGReadKey(FILE_INI, "FAVORITE MAPS", "NAME" & nFile)
		sImageFile	= CONFIGReadKey(FILE_INI, "FAVORITE MAPS", "IMAGEFILE" & nFile)
		sTabFile		= CONFIGReadKey(FILE_INI, "FAVORITE MAPS", "TABFILE" & nFile)
		sLyrSettings	= CONFIGReadKey(FILE_INI, "FAVORITE MAPS", "LAYERSETTING" & nFile)
		sLblSettings	= CONFIGReadKey(FILE_INI, "FAVORITE MAPS", "LABELSETTING" & nFile)

		Call DEBUGPrint(nCount & ": '" & sName & "', '" & sImageFile & "', '" & sTabFile & "'")
		If sName <> "" AND sImageFile <> "" AND sTabFile <> "" Then
			nCount = nCount + 1
			Redim marrFavoriteMaps(nCount)

			marrFavoriteMaps(nCount).sName		= sName
			marrFavoriteMaps(nCount).sImageFile	= FILEGetAbsolutePath(ApplicationDirectory$(), sImageFile)
			marrFavoriteMaps(nCount).sTabFile		= FILEGetAbsolutePath(ApplicationDirectory$(), sTabFile)
			marrFavoriteMaps(nCount).sLyrSettings	= sLyrSettings
			marrFavoriteMaps(nCount).sLblSettings	= sLblSettings

			Call DEBUGPrint(nCount & ": '" & marrFavoriteMaps(nCount).sImageFile & "'")

			nNoneFound = 0
		Else
			Call DEBUGPrint(nFile & ": Not Found!")
			nNoneFound = nNoneFound + 1

			If nNoneFound > 3 Then
				Exit Do
			End If
		End If
	Loop

	If CONFIGReadKey(FILE_INI, "SETTINGS", "ADDTOOWNTAB") = "1" Then
		i = i + 1
		Redim marrTabs(i)
		Redim marrGroups(i)
		Redim marrAfterControls(i)
		Redim marrBeforeControls(i)
		marrTabs(i)			= "tabFavoriteMaps"
		marrGroups(i)			= "grpFavoriteMaps"
		marrAfterControls(i)	= ""
	End If
	If CONFIGReadKey(FILE_INI, "SETTINGS", "ADDTOHOMETAB") = "1" Then
		i = i + 1
		Redim marrTabs(i)
		Redim marrGroups(i)
		Redim marrAfterControls(i)
		Redim marrBeforeControls(i)
		marrTabs(i)			= TAB_HOME
		marrGroups(i)			= TAB_GROUP_HOME_FILE
'		marrAfterControls(i)	= "HomeFileOpenGallerySplitButton"
		marrBeforeControls(i)	= "HomeSaveWorkspaceSplitButton"
	End If
	If CONFIGReadKey(FILE_INI, "SETTINGS", "ADDTOTABLETAB") = "1" Then
		i = i + 1
		Redim marrTabs(i)
		Redim marrGroups(i)
		Redim marrAfterControls(i)
		Redim marrBeforeControls(i)
		marrTabs(i)			= TAB_TABLE
		marrGroups(i)			= TAB_GROUP_TABLE_DATA
		marrAfterControls(i)	= "TableFileOpenGallerySplitButton"
	End If
	If CONFIGReadKey(FILE_INI, "SETTINGS", "ADDTOMAPTAB") = "1" Then
		i = i + 1
		Redim marrTabs(i)
		Redim marrGroups(i)
		Redim marrAfterControls(i)
		Redim marrBeforeControls(i)
		marrTabs(i)			= TAB_MAP
		marrGroups(i)			= TAB_GROUP_MAP_FILE
		marrAfterControls(i)	= "MapFileOpenGallerySplitButton"
	End If
	If CONFIGReadKey(FILE_INI, "SETTINGS", "ADDTOLAYOUTTAB") = "1" Then
		i = i + 1
		Redim marrTabs(i)
		Redim marrGroups(i)
		Redim marrAfterControls(i)
		Redim marrBeforeControls(i)
		marrTabs(i)			= TAB_LAYOUT
		marrGroups(i)			= TAB_GROUP_LAYOUT_PAGE
		marrAfterControls(i)	= "LayoutFileOpenGallerySplitButton"
	End If

	Redim marrSplitButtonCtrlIDs(i)
	Redim marrFirstControlIDs(i)

	If CONFIGReadKey(FILE_INI, "SETTINGS", "USELARGEICONS") = "1" Then
		mnImageSize = CONTROL_SIZE_LARGE
	Else
		mnImageSize = CONTROL_SIZE_SMALL
	End If

	mbShowSaveLayerSettingMenu	= (CONFIGReadKey(FILE_INI, "SETTINGS", "SHOWSAVELAYERSETTINGMENU") = "1")

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "MENUReadConfigFile")
	Call ERRShow()

End Sub