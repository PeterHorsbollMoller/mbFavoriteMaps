'*******************************************************************************
'** 	Created By Peter Horsbøll Møller, Precisely
'** 	Program:
'** 	Modul:
'**
'*******************************************************************************

'-------------------------------------
Include "MapBasic.def"
Include "Icons.def"
Include "Menu.def"
'**default constants...
Include "Library\Defaults.def"

'-------------------------------------
Include "Library\ARRAYLib.def"
Include "Library\DEBUGLib.def"
Include "Library\ERRORLib.def"
Include "Library\FILELib.def"
Include "Library\TABLELib.def"

Include "DLGFMCFavoriteMapConfig.def"

Define	CTRL_TXT_NAME				100
Define	CTRL_TXT_TABFILE			200
Define	CTRL_TXT_ICONFILE			300
Define	CTRL_CHK_SHOWONOPENLIST		400
Define	CTRL_CHK_SHOWONCUSTOMLIST	500
Define	CTRL_LST_CUSTOMGROUPNAMES	510

Declare Sub DLGFMCHandler_OnLoad
Declare Sub DLGFMCHandler_OKClicked
Declare Sub DLGFMCHandler_SelectTabFile
Declare Sub DLGFMCHandler_SelectIconFile

Dim	mtFavoriteMap As T_FAVORITE_MAP,
	marrCustomGroupNames() As String
'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGFMCFavoriteMapConfig( tFavoriteMap As T_FAVORITE_MAP) As Logical

OnError GoTo ErrorOccured

	Call DEBUGPrint("B1 Name:" & tFavoriteMap.sName & ".")

	mtFavoriteMap.sName 			= tFavoriteMap.sName
	mtFavoriteMap.sImageFile			= tFavoriteMap.sImageFile
	mtFavoriteMap.sTabFile			= tFavoriteMap.sTabFile
	mtFavoriteMap.sLyrSettings		= tFavoriteMap.sLyrSettings
	mtFavoriteMap.sLblSettings		= tFavoriteMap.sLblSettings
	mtFavoriteMap.bShowOnCustomList	= tFavoriteMap.bShowOnCustomList
	mtFavoriteMap.sCustomGroupName	= tFavoriteMap.sCustomGroupName
	mtFavoriteMap.bShowOnOpenList		= tFavoriteMap.bShowOnOpenList

DLGFMCFavoriteMapConfig = FALSE

	Dialog
		Title "Favorite Map"
		Calling DLGFMCHandler_OnLoad

		Control StaticText	Position 5,7
			Title "Name:"
		Control EditText	Position 40,5		ID CTRL_TXT_NAME
			Width 250
			Value mtFavoriteMap.sName

		Control StaticText	Position 5,22
			Title "File:"
		Control EditText	Position 40,20		ID CTRL_TXT_TABFILE
			Width 235
			Value FILEGetRelativePath(ApplicationDirectory$(), mtFavoriteMap.sTabFile)
		Control Button		Position 280,20
			Width 15		Height 12
			Title "..."
			Calling DLGFMCHandler_SelectTabFile

		Control StaticText	Position 5,37
			Title "Icon:"
		Control EditText	Position 40,35		ID CTRL_TXT_ICONFILE
			Width 235
			Value FILEGetRelativePath(ApplicationDirectory$(), mtFavoriteMap.sImageFile)
		Control Button		Position 280,35
			Width 15		Height 12
			Title "..."
			Calling DLGFMCHandler_SelectIconFile

		Control CheckBox	Position 40,60		ID CTRL_CHK_SHOWONOPENLIST
			Title "Add to Open > Base Maps"
			Value mtFavoriteMap.bShowOnOpenList

		Control CheckBox	Position 40,75		ID CTRL_CHK_SHOWONCUSTOMLIST
			Title "Add to Favorite Maps undet the selected group"
			Value mtFavoriteMap.bShowOnCustomList

		Control PopUpMenu	Position 60,90		ID CTRL_LST_CUSTOMGROUPNAMES
			Width 240
			Title From Variable marrCustomGroupNames
			Value Maximum(ARRAYFindValueString(marrCustomGroupNames, mtFavoriteMap.sCustomGroupName), 1)

		Control OKButton
			Calling DLGFMCHandler_OKClicked
		Control CancelButton

	If not CommandInfo(CMD_INFO_DLG_OK) Then
		Exit Function
	End If

	Call DEBUGPrint("B2a Name:" & mtFavoriteMap.sName & ".")

	tFavoriteMap.sName 				= mtFavoriteMap.sName
	tFavoriteMap.sImageFile			= mtFavoriteMap.sImageFile
	tFavoriteMap.sTabFile			= mtFavoriteMap.sTabFile
	tFavoriteMap.sLyrSettings		= mtFavoriteMap.sLyrSettings
	tFavoriteMap.sLblSettings		= mtFavoriteMap.sLblSettings
	tFavoriteMap.bShowOnCustomList	= mtFavoriteMap.bShowOnCustomList
	tFavoriteMap.sCustomGroupName		= mtFavoriteMap.sCustomGroupName
	tFavoriteMap.bShowOnOpenList		= mtFavoriteMap.bShowOnOpenList

	Call DEBUGPrint("B2b Name:" & tFavoriteMap.sName & ".")

DLGFMCFavoriteMapConfig = TRUE

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGFMCFavoriteMapConfig")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGFMCHandler_OnLoad

OnError GoTo ErrorOccured

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGFMCHandler_OnLoad")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGFMCHandler_OKClicked

OnError GoTo ErrorOccured

	mtFavoriteMap.sName 			= ReadControlValue(CTRL_TXT_NAME)
	If mtFavoriteMap.sName = "" Then
		Note "Please enter a name for your favorite map!"
		Alter Control CTRL_TXT_NAME Active
		Dialog Preserve
		Exit Sub
	End If
	mtFavoriteMap.sImageFile			= ReadControlValue(CTRL_TXT_ICONFILE)
	If mtFavoriteMap.sImageFile = "" Then
		Note "Please select an icon for your favorite map!"
		Alter Control CTRL_TXT_ICONFILE Active
		Dialog Preserve
		Exit Sub
	End If
	mtFavoriteMap.sImageFile			= FILEGetAbsolutePath(ApplicationDirectory$(), mtFavoriteMap.sImageFile)
	If not FileExists(mtFavoriteMap.sImageFile) Then
		Note "The selected icon file does not exist: " & mtFavoriteMap.sImageFile
		Alter Control CTRL_TXT_ICONFILE Active
		Dialog Preserve
		Exit Sub
	End If

	mtFavoriteMap.sTabFile			= ReadControlValue(CTRL_TXT_TABFILE)
	If mtFavoriteMap.sTabFile = "" Then
		Note "Please select an icon for your favorite map!"
		Alter Control CTRL_TXT_TABFILE Active
		Dialog Preserve
		Exit Sub
	End If
	mtFavoriteMap.sTabFile			= FILEGetAbsolutePath(ApplicationDirectory$(), mtFavoriteMap.sTabFile)
	If not FileExists(mtFavoriteMap.sTabFile) Then
		Note "The selected tab file does not exist: " & mtFavoriteMap.sTabFile
		Alter Control CTRL_TXT_TABFILE Active
		Dialog Preserve
		Exit Sub
	End If

'	mtFavoriteMap.sLyrSettings		= tFavoriteMap.sLyrSettings
'	mtFavoriteMap.sLblSettings		= tFavoriteMap.sLblSettings

	mtFavoriteMap.bShowOnOpenList		= ReadControlValue(CTRL_CHK_SHOWONOPENLIST)
	mtFavoriteMap.bShowOnCustomList	= ReadControlValue(CTRL_CHK_SHOWONCUSTOMLIST)

	If ReadControlValue(CTRL_LST_CUSTOMGROUPNAMES) = 0 Or ReadControlValue(CTRL_LST_CUSTOMGROUPNAMES) > Ubound(marrCustomGroupNames) Then
		Note "Please select a custom group name from the list!"
		Alter Control CTRL_LST_CUSTOMGROUPNAMES Active
		Dialog Preserve
		Exit Sub
	End If

	mtFavoriteMap.sCustomGroupName	= marrCustomGroupNames(ReadControlValue(CTRL_LST_CUSTOMGROUPNAMES))

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGFMCHandler_OKClicked")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGFMCHandler_SelectTabFile

Dim	sFile As String

OnError GoTo ErrorOccured

	sFile			= ReadControlValue(CTRL_TXT_TABFILE)
	If sFile <> "" Then
		sFile	= FILEGetAbsolutePath(ApplicationDirectory$(), sFile)
	End If

	sFile = FileOpenDlg(PathToDirectory$(sFile), PathToFileName$(sFile), "TAB", "Select tab file to open using this shortcut...")
	If sFile = "" Then
		Exit Sub
	End If

	Alter Control CTRL_TXT_TABFILE Value FILEGetRelativePath(ApplicationDirectory$(), sFile)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGFMCHandler_SelectTabFile")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGFMCHandler_SelectIconFile

Dim	sFile As String

OnError GoTo ErrorOccured

	sFile			= ReadControlValue(CTRL_TXT_ICONFILE)
	If sFile <> "" Then
		sFile	= FILEGetAbsolutePath(ApplicationDirectory$(), sFile)
	End If

	sFile = FileOpenDlg(PathToDirectory$(sFile), PathToFileName$(sFile), "PNG", "Select icon file to use for this shortcut...")
	If sFile = "" Then
		Exit Sub
	End If

	Alter Control CTRL_TXT_ICONFILE Value FILEGetRelativePath(ApplicationDirectory$(), sFile)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGFMCHandler_SelectIconFile")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGFMCAddToCustomGroupNames( ByVal sGroupName As String)

OnError GoTo ErrorOccured

	Call ARRAYAddValueString(marrCustomGroupNames, sGroupName)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGFMCAddToCustomGroupNames")
	Call ERRShow()

End Sub

